use std::str::FromStr;
use std::rc::Rc;
use crate::micro_c::{
    Block, Declaration, Statement, Expression, Type, BasicType, BinaryOperator,
    UnaryOperator, Lvalue
};

grammar;

pub Program = {
    Block
};

Block: Block<'input> = {
    LBRACE <Declarations> <Statements> RBRACE => Block::new(<>)
};

Declarations: Option<Rc<Declaration<'input>>> = {
    MultipleDeclaration => Some(<>),
    => None
};

MultipleDeclaration: Rc<Declaration<'input>> = {
    SingleDeclaration => <>,
    SingleDeclaration MultipleDeclaration => Rc::new(Declaration::Composite(<>))
};

SingleDeclaration: Rc<Declaration<'input>> = {
    <SimpleDeclaration> SEMI => Rc::new(<>),
};

SimpleDeclaration: Declaration<'input> = {
    <Types> <IDENTIFIER> => Declaration::Variable(<>),
    <Types> <IDENTIFIER> LBRACKET <Constant> RBRACKET => Declaration::Array(<>),
};

Statements: Rc<Statement<'input>> = {
    SingleStatement => Rc::new(<>),
    <s: SingleStatement> <r: Statements> => Rc::new(Statement::Composite(Rc::new(s), r))
};

SingleStatement: Statement<'input> = {
    <SimpleStatements> SEMI => <>,
    IF LPAREN <e: Expression> RPAREN <b: Block> => Statement::IfElse(e,Rc::new(b), None),
    IF LPAREN <e: Expression> RPAREN <b_then: Block> ELSE <b_else: Block>
        => Statement::IfElse(e,Rc::new(b_then),Some(Rc::new(b_else))),
    WHILE LPAREN <e: Expression> RPAREN <b: Block> => Statement::While(e,Rc::new(b)),
    Block => Statement::Scope(Rc::new(<>)),
};

SimpleStatements: Statement<'input> = {
    <Lvalue> ASSIGN <Expression> => Statement::Assign(<>),
    READ <Lvalue> => Statement::Read(<>),
    WRITE <Expression> => Statement::Write(<>),
    BREAK => Statement::Break,
    CONTINUE => Statement::Continue,
};

Lvalue: Rc<Lvalue<'input>> = {
    <IDENTIFIER>  => Rc::new(Lvalue::Variable(false, <>)),
    MUL <IDENTIFIER>  => Rc::new(Lvalue::Variable(true, <>)),
    <IDENTIFIER> LBRACKET <Expression> RBRACKET => Rc::new(Lvalue::ArrayAccess(false, <>)),
    MUL <IDENTIFIER> LBRACKET <Expression> RBRACKET => Rc::new(Lvalue::ArrayAccess(true, <>)),
};

Expression: Rc<Expression<'input>> = {
    SimpleExpression => <>,
    <e1: SimpleExpression> <o: BinaryOperator> <e2: Expression> =>
        Rc::new(Expression::Binary(e1, o, e2)),
};

SimpleExpression: Rc<Expression<'input>> ={
    LPAREN <Expression> RPAREN => <>,
    Constant => Rc::new(Expression::Constant(<>)),
    <IDENTIFIER> => Rc::new(Expression::Variable(<>)),
    <IDENTIFIER> LBRACKET <Expression> RBRACKET => Rc::new(Expression::ArrayAccess(<>)),
    <o: UnaryOperator> <e: SimpleExpression> => Rc::new(Expression::Unary(o, e)),
};

Types: Type = {
    <t: BasicType> => Type{is_pointer: false, is_mutable: true, basic_type: t},
    CONST <t: BasicType> => Type{is_pointer: false, is_mutable: false, basic_type: t},
    <t: BasicType> MUL => Type{is_pointer: true, is_mutable: true, basic_type: t},
    CONST <t: BasicType> MUL => Type{is_pointer: true, is_mutable: false, basic_type: t},
};

BasicType: BasicType = {
    INT => BasicType::Int,
    VOID => BasicType::Void
};

Constant: i32 = INT_LITERAL => i32::from_str(<>).unwrap();

BinaryOperator: BinaryOperator = {
    PLUS => BinaryOperator::Plus,
    MINUS => BinaryOperator::Minus,
    MUL => BinaryOperator::Multiply,
    DIV => BinaryOperator::Division,
    LT => BinaryOperator::LessThan,
    GT => BinaryOperator::GreaterThan,
    LE => BinaryOperator::LessOrEqual,
    GE => BinaryOperator::GreaterOrEqual,
    EQ => BinaryOperator::Equal,
    NEQ => BinaryOperator::NotEqual,
    AND => BinaryOperator::And,
    OR => BinaryOperator::Or
};

UnaryOperator: UnaryOperator<'input> = {
    MINUS => UnaryOperator::Negative,
    NOT => UnaryOperator::Not,
    MUL => UnaryOperator::Deref,
    AND <LIFETIME> => UnaryOperator::BorrowMut(<>),
    AND <LIFETIME> CONST => UnaryOperator::BorrowConst(<>),
};

// -------------- Lexer Tokens -----------------------
match {
    // Operators
    "&" => AND,
    "|" => OR,
    "=" => ASSIGN,
    ">" => GT,
    ">=" => GE,
    "<" => LT,
    "<=" => LE,
    "==" => EQ,
    "!=" => NEQ,
    "+" => PLUS,
    "-" => MINUS,
    "*" => MUL,
    "/" => DIV,
    "!" => NOT,

    // Punctuation
    "(" => LPAREN,
    ")" => RPAREN,
    "{" => LBRACE,
    "}" => RBRACE,
    "[" => LBRACKET,
    "]" => RBRACKET,
    ":" => COLON,
    ";" => SEMI,

    //Keywords
    "if" => IF,
    "else" => ELSE,
    "while" => WHILE,
    "continue" => CONTINUE,
    "break" => BREAK,
    "write" => WRITE,
    "read" => READ,
    "int" => INT,
    "void" => VOID,
    "const" => CONST,
} else {

    // Integer Literal
    r"0|[1-9][0-9]*" => INT_LITERAL,

    //lifetime
    r"'[a-zA-Z]+" => LIFETIME,
    // Identifier
    r"[a-zA-Z]+" => IDENTIFIER,
    _
}